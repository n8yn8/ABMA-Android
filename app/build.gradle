apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "com.n8yn8.abma"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 31
        versionName "2.2.1"
        multiDexEnabled true

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
//        testInstrumentationRunner "com.n8yn8.abma.test.util.FakeRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            firebaseCrashlytics {
                // If you don't need crash reporting for your debug build,
                // you can speed up your build by disabling mapping file uploading.
                mappingFileUploadEnabled false
            }
        }
    }
    lintOptions {
        warning 'InvalidPackage'
    }

    sourceSets {
        androidTest {
            assets.srcDirs += files("$projectDir/schemas".toString()) // used by Room, to test migrations
            java.srcDirs += "src/sharedTest/java"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
        }
    }

    testOptions {
        animationsDisabled = true
        unitTests {
            returnDefaultValues = true
            //Robolectric 4 requirement
//            includeAndroidResources = true
        }
    }

    lintOptions {
        // firebase-messaging dependency causes this
        disable "IncompatibleIidVersion"
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.backendless:backendless:5.7.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.google.firebase:firebase-core:17.4.3'
    implementation 'com.google.firebase:firebase-messaging:20.2.0' //Check if "IncompatibleIidVersion" can be removed
    implementation 'com.google.firebase:firebase-crashlytics:17.0.1'
    implementation 'com.google.firebase:firebase-analytics:17.4.3'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    //Room dependencies
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    def lifecycle_version = "2.2.0"
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    def coroutines_version = "1.3.5"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    implementation "androidx.core:core-ktx:1.3.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"

    implementation "com.google.dagger:hilt-android:$versions.hilt.base"
    kapt "com.google.dagger:hilt-android-compiler:$versions.hilt.base"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$versions.hilt.life_cycle"
    kapt "androidx.hilt:hilt-compiler:$versions.hilt.life_cycle"

    androidTestImplementation 'io.mockk:mockk-android:1.9.3'
    testImplementation 'io.mockk:mockk:1.9.3'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0' //Includes Mockito
    testImplementation 'org.robolectric:robolectric:4.3.1'
}
